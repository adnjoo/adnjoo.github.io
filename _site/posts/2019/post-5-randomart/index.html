<!DOCTYPE html>
<html lang="en">
<head>
  <meta charset="utf-8">
  <meta name="viewport" content="width=device-width, initial-scale=1" />
  <title>drew's blog</title>

  <link rel="stylesheet" href="./css/site.css">
  <link href="https://fonts.googleapis.com/css?family=Roboto+Slab:700|Roboto&display=fallback" rel="stylesheet">
</head>
<body>
<header>
  <a href="./" class="link--home">drew's blog</a>
  <a href="./About" class="link--about">About</a>
</header>
<main>
  <article>
  <h1>random art</h1>
  <time datetime="2019-10-08T00:00:00.000Z">October 8, 2019</time>
  <!-- Excerpt Start -->
<p>digital art</p>
<!-- Excerpt End -->
<style>
@media all and (max-width:480px) {

    img, canvas {display:block;margin:0 auto;}
}
p {
overflow: auto;
}
</style>
<p>just some random art on <a href="https://openprocessing.org/">processing</a></p>
<script src='https://cdnjs.cloudflare.com/ajax/libs/processing.js/1.6.6/processing.js'></script>
<script type="text/processing" data-processing-target="mycanvas">

ArrayList<Particle> pts;
boolean onPressed, showInstruction;
PFont f;

void setup() {
  size(360, 360, P2D);
  smooth();
  frameRate(30);
  colorMode(RGB);
  rectMode(CENTER);

  pts = new ArrayList<Particle>();

  showInstruction = true;
  f = createFont("Calibri", 24, true);

  background(255);
}

void draw() {
  if (showInstruction) {
    background(255);
    fill(128);
    textAlign(CENTER, CENTER);
    textFont(f);
    textLeading(36);
    text("Drag and draw." + "\n" +
      "Press 'c' to clear the stage." + "\n"
      , width*0.5, height*0.5);
  }

  if (onPressed) {
    for (int i=0;i<10;i++) {
      Particle newP = new Particle(mouseX, mouseY, i+pts.size(), i+pts.size());
      pts.add(newP);
    }
  }

  for (int i=0; i<pts.size(); i++) {
    Particle p = pts.get(i);
    p.update();
    p.display();
  }

  for (int i=pts.size()-1; i>-1; i--) {
    Particle p = pts.get(i);
    if (p.dead) {
      pts.remove(i);
    }
  }
}

void mousePressed() {
  onPressed = true;
  if (showInstruction) {
    background(255);
    showInstruction = false;
  }
}

void mouseReleased() {
  onPressed = false;
}

void keyPressed() {
  if (key == 'c') {
    for (int i=pts.size()-1; i>-1; i--) {
      Particle p = pts.get(i);
      pts.remove(i);
    }
    background(255);
  }
}

class Particle{
  PVector loc, vel, acc;
  int lifeSpan, passedLife;
  boolean dead;
  float alpha, weight, weightRange, decay, xOffset, yOffset;
  color c;

  Particle(float x, float y, float xOffset, float yOffset){
    loc = new PVector(x,y);

    float randDegrees = random(360);
    vel = new PVector(cos(radians(randDegrees)), sin(radians(randDegrees)));
    vel.mult(random(5));

    acc = new PVector(0,0);
    lifeSpan = int(random(30, 90));
    decay = random(0.75, 0.9);
    c = color(random(255),random(255),255);
    weightRange = random(3,50);

    this.xOffset = xOffset;
    this.yOffset = yOffset;
  }

  void update(){
    if(passedLife>=lifeSpan){
      dead = true;
    }else{
      passedLife++;
    }

    alpha = float(lifeSpan-passedLife)/lifeSpan * 70+50;
    weight = float(lifeSpan-passedLife)/lifeSpan * weightRange;

    acc.set(0,0);

    float rn = (noise((loc.x+frameCount+xOffset)*0.01, (loc.y+frameCount+yOffset)*0.01)-0.5)*4*PI;
    float mag = noise((loc.y+frameCount)*0.01, (loc.x+frameCount)*0.01);
    PVector dir = new PVector(cos(rn),sin(rn));
    acc.add(dir);
    acc.mult(mag);

    float randDegrees = random(360);
    PVector randV = new PVector(cos(radians(randDegrees)), sin(radians(randDegrees)));
    randV.mult(0.5);
    acc.add(randV);

    vel.add(acc);
    vel.mult(decay);
    vel.limit(3);
    loc.add(vel);
  }

  void display(){
    strokeWeight(weight+1.5);
    stroke(0, alpha);
    point(loc.x, loc.y);

    strokeWeight(weight);
    stroke(c);
    point(loc.x, loc.y);
  }
}

</script>
<p><canvas style='border:1px solid black; border-radius:10px; box-shadow: 5px 10px' id="mycanvas"></canvas></p>
<p><img src="https://i.postimg.cc/mk2f8cs7/sunny-1.png" alt=""></p>

</article>
</main>
<footer>&copy; drew's blog</footer>
</body>
</html>